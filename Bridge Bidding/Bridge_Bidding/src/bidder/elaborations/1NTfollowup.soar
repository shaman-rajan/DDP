#################### Pattern checking ####################

sp {expand*stayman-1NT-initiate*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name stayman-1NT-initiate)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*transfer-to-heart*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name transfer-to-heart)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*transfer-to-spade*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name transfer-to-spade)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*minor-suit-transfer*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name minor-suit-transfer)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*simple-invitation-in-NT*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name simple-invitation-in-NT)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*simple-invitation-in-minor*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name simple-invitation-in-minor)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*slam-invitation-in-major*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name slam-invitation-in-major)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*3N-over-1N*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name 3N-over-1N)
-->
    (<n> ^pattern |PASS 1N *|)
} 

sp {expand*gerber*pattern #want slam
    (state <s> ^name bidder ^node <n>)
    (<n> ^name gerber)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*game-major-over-1NT*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name game-major-over-1NT)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*slam-invitation-4NT*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name slam-invitation-4NT)
-->
    (<n> ^pattern |PASS 1N *|)
}

sp {expand*game-minor-over-1NT*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name game-minor-over-1NT)
-->
    (<n> ^pattern |PASS 1N *|)
}

################### Constraint checking ###################

sp {expand*stayman-1NT-initiate
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name stayman-1NT-initiate
         ^pattern-checked true)
    (<v> ^hcp_low {<hcp_low> >= 8}
         ^lmaj_low 4)
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 2C ^meaning <mean>)
    (<mean> ^hcp_low 8
            ^lmaj_low 4
            ^message |Initiating stayman|)
}

sp {expand*transfer-to-heart
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name transfer-to-heart
         ^pattern-checked true)
    (<v> ^heart.num_low {<hnl> >= 5})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 2D ^meaning <mean>)
    (<mean> ^heart.num_low 5
            ^message |Transferring to heart|)
}

sp {expand*transfer-to-spade
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name transfer-to-spade
         ^pattern-checked true)
    (<v> ^spade.num_low {<hnl> >= 5})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 2H ^meaning <mean>)
    (<mean> ^spade.num_low 5
            ^message |Transferring to spade|)
}

sp {expand*minor-suit-transfer
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name minor-suit-transfer
         ^pattern-checked true)
    (<v> ^hcp_high {<hcp_high> <= 7} 
         ^lmin_low {<lmin_low> >= 6})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 2S ^meaning <mean>)
    (<mean> ^hcp_high 7
            ^lmin_low 6
            ^message |Transferring to minor|)
}

sp {expand*simple-invitation-in-NT
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name simple-invitation-in-NT
         ^pattern-checked true)
    (<v> ^lmaj_high {<lmaj_high> <= 3}
         ^hcp_low {<hcp_low> >= 8}
         ^hcp_high {<hcp_high> <= 9})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 2N ^meaning <mean>)
    (<mean> ^lmaj_high 3
            ^hcp_low 8
            ^hcp_high 9
            ^team.invited true
            ^message |Inviting partner for game in NT|)
}
        
sp {expand*simple-invitation-in-minor
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name simple-invitation-in-minor
         ^pattern-checked true
         ^minor <minor>)
    (<v> ^lmaj_high {<lmaj_high> <= 3}
         ^<minor>.num_low {<mnl> >= 6}
         ^hcp_low {<hcp_low> >= 8}
         ^hcp_high {<hcp_high> <= 9})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 3 <minor>) ^meaning <mean>)
    (<mean> ^lmaj_high 3
            ^<minor>.num_low 6
            ^hcp_low 8
            ^hcp_high 9
            ^team.trump <minor>
            ^team.invited true
            ^message |Inviting partner for game in minor|)
}

sp {expand*slam-invitation-in-major
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name slam-invitation-in-major
         ^pattern-checked true
         ^major <major>)
    (<v> ^<major>.num_low {<mnl> >= 6}
         ^hcp_low {<hcp_low> >= 10})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 3 <major>) ^meaning <mean>)
    (<mean> ^<major>.num_low 6
            ^team.game_forced 1
            ^team.trump <major>
            ^message |Forcing game and inviting partner for slam in major|)
}

sp {expand*3N-over-1N
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name 3N-over-1N
         ^pattern-checked true)
    (<v> ^hcp_low {<hcp_low> >= 10}
         ^hcp_high {<hcp_high> <= 13}
         ^lmaj_high {<lmaj_high> <= 3})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 3N ^meaning <mean>)
    (<mean> ^hcp_low 10
            ^hcp_high 13
            ^lmaj_high 3
            ^message |Hand is strong enough for bidding a game|)
}

sp {expand*gerber
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name gerber
         ^pattern-checked true)
    (<v> ^hcp_low {<hcp_low> >= 20})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 4C ^meaning <mean>)
    (<mean> ^hcp_low 20)
}

sp {expand*game-major-over-1NT
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name game-major-over-1NT
         ^pattern-checked true
         ^major <major>)
    (<v> ^hcp_low {<hcp_low> >= 7}
         ^hcp_high {<hcp_high> <= 8}
         ^<major>.num_low {<mnl> >= 6})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 4 <major>) ^meaning <mean>)
    (<mean> ^hcp_low 7
            ^hcp_high 8
            ^<major>.num_low 6
            ^message |Hand is strong enough for bidding a game|)
}

sp {expand*slam-invitation-4NT
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name slam-invitation-4NT
         ^pattern-checked true)
    (<v> ^hcp_low {<hcp_low> >= 14}
         ^hcp_high {<hcp_high> <= 15}
         ^lmaj_high {<lmaj_high> <= 3})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 4N ^meaning <mean>)
    (<mean> ^hcp_low 14
            ^hcp_high 15
            ^lmaj_high 3)
}

sp {expand*game-minor-over-1NT
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name game-minor-over-1NT
         ^pattern-checked true
         ^minor <minor>)
    (<v> ^<minor>.num_low {<mnl> >= 6}
         ^<minor>.hcp_low {<hcp_low> >= 6}
         ^ratio_low {<rl> >= 0.8})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 5 <minor>) ^meaning <mean>)
    (<mean> ^<minor>.num_low 6
            ^<minor>.hcp_low 6
            ^ratio_low 0.8
            ^message |Hand is strong enough for bidding a game|)
}
