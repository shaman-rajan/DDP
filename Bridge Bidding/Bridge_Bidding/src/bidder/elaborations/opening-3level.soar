#################### Pattern checking ####################

sp {expand*opening-3m*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name opening-3m)
-->
    (<n> ^pattern *)
}

sp {expand*opening-3M*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name opening-3M)
-->
    (<n> ^pattern *)
}

sp {expand*opengin-3N*pattern
    (state <s> ^name bidder ^node <n>)
    (<n> ^name opening-3N)
-->
    (<n> ^pattern *)
}

################### Constraint checking ###################

sp {expand*opening-3m
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name opening-3m
         ^pattern-checked true
         ^minor <minor>)
    (<v> ^hcp_low {<hcp_low> >= 5}
         ^hcp_high {<hcp_high> <= 10}
         ^<minor>.num_low 7
         ^<minor>.num_high 7
         ^lmaj_high {<lmaj_high> <= 3}
         ^<minor>.unlos_high {<un_h> <= 2})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 3 <minor>) ^meaning <mean>)
    (<mean> ^hcp_low 5
            ^hcp_high 10
            ^<minor>.num_low 7
            ^<minor>.num_high 7
            ^lmaj_high 3
            ^<minor>.unlos_high 2)
    # No children as of now, otherwise would add the children attribute
}

sp {expand*opening-3M
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name opening-3M
         ^pattern-checked true
         ^major <major>
         ^major {<other-major> <> <major>})
    (<v> ^hcp_low {<hcp_low> >= 5}
         ^hcp_high {<hcp_high> <= 10}
         ^<major>.num_low 7
         ^<major>.num_high 7
         ^<other-major>.num_high {<other-high> <= 3}
         ^<major>.unlos_high {<un_h> <= 2})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form (concat 3 <major>) ^meaning <mean>)
    (<mean> ^hcp_low 5
            ^hcp_high 10
            ^<major>.num_low 7
            ^<major>.num_high 7
            ^lmaj_low 7
            ^lmaj_high 7
            ^<major>.unlos_high 2)
    # No children as of now, otherwise would add the children attribute
}

sp {expand*opening-3N
    (state <s> ^name bidder ^node <n> ^myview <v>)
    (<n> ^name opening-3N
         ^pattern-checked true
         ^minor <minor>)
    (<v> ^<minor>.quality {<mquality> >= 8}
         ^controls_high {<ch> <= 3})
-->
    (<n> ^status visited ^bid <b>)
    (<b> ^short-form 3N ^meaning <mean>)
    (<mean> ^controls_high 3
            ^spade.controls_high 0
            ^heart.controls_high 0)
}